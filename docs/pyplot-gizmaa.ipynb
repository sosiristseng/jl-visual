{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# PythonPlot.jl gallery\n",
    "\n",
    "From this awesome gist: https://gist.github.com/gizmaa/7214002"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using Random\n",
    "import PythonPlot as plt\n",
    "plt.using3D()\n",
    "Random.seed!(2021)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Annotations"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using Dates, LaTeXStrings\n",
    "import PythonPlot as plt\n",
    "\n",
    "# Data: Generate an hour of data at 10Hz.\n",
    "\n",
    "# Generate time array\n",
    "x = collect(DateTime(2013,10,4):Dates.Millisecond(100):DateTime(2013,10,4,1))\n",
    "# Convert time from milliseconds from day 0 to days from day 0\n",
    "x = Dates.value.(x)/1000/60/60/24\n",
    "y = sin.(2*pi*collect(0:2*pi/(length(x)+1):2*pi-(2*pi/length(x))))\n",
    "\n",
    "dx = maximum(x) - minimum(x)\n",
    "dy = maximum(y) - minimum(y)\n",
    "\n",
    "\n",
    "y2 = 10rand(21) .- 3\n",
    "\n",
    "x2 = collect(minimum(x):dx/20:maximum(x))\n",
    "x3 = collect(minimum(x):dx/20:maximum(x))\n",
    "\n",
    "y3 = 10rand(21) .- 3\n",
    "\n",
    "fig, ax = plt.subplots(figsize=(8, 8))\n",
    "\n",
    "# Plot a basic line\n",
    "ax.plot_date(x,y, linestyle=\"-\", marker=\"None\", label=\"Test Plot\")\n",
    "# Fit the axis tightly to the plot\n",
    "ax.axis(\"tight\")\n",
    "ax.set_title(\"U Component of Wind\")\n",
    "ax.grid(\"on\")\n",
    "ax.legend(loc=\"upper right\",fancybox=\"true\")\n",
    "\n",
    "# Text Styling\n",
    "font1 = Dict(\"family\"=>\"serif\",\n",
    "             \"color\"=>\"darkred\",\n",
    "             \"weight\"=>\"normal\",\n",
    "             \"size\"=>16)\n",
    "ax.set_xlabel(\"Time\", fontdict=font1)        ## X Axis font formatting\n",
    "ax.set_ylabel(\"Velocity (m/s)\")\n",
    "plt.setp(ax.get_yticklabels(),fontsize=24,color=\"blue\") ## Y Axis font formatting\n",
    "\n",
    "# Arrow Tests: This arrows orient toward the x-axis, the more horizontal they are the more skewed they look\n",
    "\n",
    "ax.arrow(x[convert(Int64,floor(length(x)/2))],\n",
    "        0.4,\n",
    "        0.0009,\n",
    "        0.4,\n",
    "        head_width=0.001,\n",
    "        width=0.00015,\n",
    "        head_length=0.07,\n",
    "        overhang=0.5,\n",
    "        head_starts_at_zero=\"true\",\n",
    "        facecolor=\"red\")\n",
    "\n",
    "ax.arrow(x[convert(Int64,floor(0.3length(x)))]-0.25dx,\n",
    "        y[convert(Int64,floor(0.3length(y)))]+0.25dy,\n",
    "        0.25dx,\n",
    "        -0.25dy,\n",
    "        head_width=0.001,\n",
    "        width=0.00015,\n",
    "        head_length=0.07,\n",
    "        overhang=0.5,\n",
    "        head_starts_at_zero=\"true\",\n",
    "        facecolor=\"red\",\n",
    "        length_includes_head=\"true\")\n",
    "\n",
    "# Text Annotation Tests\n",
    "\n",
    "ax.annotate(\"Look, data!\",\n",
    "    xy=[x[convert(Int64,floor(length(x)/4.1))];y[convert(Int64,floor(length(y)/4.1))]],\n",
    "    xytext=[x[convert(Int64,floor(length(x)/4.1))]+0.1dx;y[convert(Int64,floor(length(y)/4.1))]+0.1dy],\n",
    "    xycoords=\"data\",\n",
    "    arrowprops=Dict(\"facecolor\"=>\"black\")) # Julia dictionary objects are automatically converted to Python object when they pass into a PythonPlot function\n",
    "ax.annotate(\"Figure Top Right\",\n",
    "    xy=[1;1],\n",
    "    xycoords=\"figure fraction\",\n",
    "    xytext=[0,0],\n",
    "    textcoords=\"offset points\",\n",
    "    ha=\"right\",\n",
    "    va=\"top\")\n",
    "ax.annotate(L\"$\\int x = \\frac{x^2}{2} + C$\",\n",
    "    xy=[1;0],\n",
    "    xycoords=\"axes fraction\",\n",
    "    xytext=[-10,10],\n",
    "    textcoords=\"offset points\",\n",
    "    fontsize=30.0,\n",
    "    ha=\"right\",\n",
    "    va=\"bottom\")\n",
    "\n",
    "fig.autofmt_xdate(bottom=0.2,rotation=30,ha=\"right\")\n",
    "\n",
    "fig"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Axis placement"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "import PythonPlot as plt\n",
    "\n",
    "# Data\n",
    "x = 0:pi/50:2pi\n",
    "y = sin.(x)\n",
    "\n",
    "fig, axs = plt.subplots(2, 1)\n",
    "\n",
    "ax = axs[0]\n",
    "\n",
    "ax.plot(x,y)\n",
    "ax.axis(\"tight\")\n",
    "ax.spines[\"top\"].set_visible(false) ## Hide the top edge of the axis\n",
    "ax.spines[\"right\"].set_visible(false) ## Hide the right edge of the axis\n",
    "ax.spines[\"left\"].set_position(\"center\") ## Move the right axis to the center\n",
    "ax.spines[\"bottom\"].set_position(\"center\") ## Most the bottom axis to the center\n",
    "ax.xaxis.set_ticks_position(\"bottom\") ## Set the x-ticks to only the bottom\n",
    "ax.yaxis.set_ticks_position(\"left\") ## Set the y-ticks to only the left\n",
    "\n",
    "ax = axs[1]\n",
    "\n",
    "ax.plot(x,y)\n",
    "ax.axis(\"tight\")\n",
    "ax.spines[\"top\"].set_visible(false) ## Hide the top edge of the axis\n",
    "ax.spines[\"right\"].set_visible(false) ## Hide the right edge of the axis\n",
    "ax.xaxis.set_ticks_position(\"bottom\")\n",
    "ax.yaxis.set_ticks_position(\"left\")\n",
    "ax.spines[\"left\"].set_position((\"axes\",-0.03)) ## Offset the left scale from the axis\n",
    "ax.spines[\"bottom\"].set_position((\"axes\",-0.05)) ## Offset the bottom scale from the axis\n",
    "ax.set_xlabel(\"X Axis\")\n",
    "ax.set_ylabel(\"Y Axis\")\n",
    "\n",
    "fig"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Bar plot"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "import PythonPlot as plt\n",
    "\n",
    "x = [1:1:50;]\n",
    "y = 100*rand(50)\n",
    "\n",
    "fig, axs = plt.subplots(2, 1, figsize=(8,8))\n",
    "\n",
    "ax = axs[0]\n",
    "\n",
    "ax.bar(x,y,color=\"#0f87bf\",align=\"center\",alpha=0.4)\n",
    "ax.axis(\"tight\")\n",
    "ax.grid(\"on\")\n",
    "ax.set_title(\"Vertical Bar Plot\")\n",
    "ax.set_xlabel(\"X\")\n",
    "ax.set_ylabel(\"Y\")\n",
    "\n",
    "ax = axs[1]\n",
    "ax.barh(x,y,color=\"#0f87bf\",align=\"center\",alpha=0.4)\n",
    "ax.axis(\"tight\")\n",
    "ax.set_title(\"Horizontal Bar Plot\")\n",
    "ax.grid(\"on\")\n",
    "ax.set_xlabel(\"X\")\n",
    "ax.set_ylabel(\"Y\")\n",
    "\n",
    "fig.suptitle(\"Bar Plot Examples\")\n",
    "\n",
    "fig"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Broken axis subplots"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using PythonCall\n",
    "import PythonPlot as plt\n",
    "\n",
    "axes_grid1 = pyimport(\"mpl_toolkits.axes_grid1\")\n",
    "\n",
    "x = rand(100)\n",
    "y = rand(100)\n",
    "y2 = rand(100).+10\n",
    "\n",
    "fig, axes = plt.subplots(2, 1, figsize=(8, 8), sharex=true)\n",
    "\n",
    "ax = axes[0]\n",
    "divider = axes_grid1.make_axes_locatable(ax)\n",
    "ax2 = divider.new_vertical(size=\"100%\", pad=0.1)\n",
    "fig.add_axes(ax2)\n",
    "\n",
    "# Lower Portion of First Plot\n",
    "ax.scatter(x, y)\n",
    "ax.set_ylim(0, 1)\n",
    "ax.spines[\"top\"].set_visible(false)\n",
    "\n",
    "# Upper Portion of First Plot\n",
    "ax2.scatter(x, y2)\n",
    "ax2.set_ylim(10, 11)\n",
    "ax2.tick_params(bottom=\"off\", labelbottom=\"off\")\n",
    "ax2.spines[\"bottom\"].set_visible(false)\n",
    "\n",
    "fig"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Add Line Break Markings: https://matplotlib.org/examples/pylab_examples/broken_axis.html"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Upper Line Break Markings\n",
    "d = 0.015  # how big to make the diagonal lines in axes coordinates\n",
    "ax2.plot((-d, +d), (-d, +d), transform=ax2.transAxes, color=\"k\", clip_on=false,linewidth=0.8)        ## Left diagonal\n",
    "ax2.plot((1 - d, 1 + d), (-d, +d), transform=ax2.transAxes, color=\"k\", clip_on=false,linewidth=0.8)  ## Right diagonal\n",
    "\n",
    "# Lower Line Break Markings\n",
    "ax.plot((-d, +d), (1 - d, 1 + d), transform=ax.transAxes, color=\"k\", clip_on=false,linewidth=0.8)  ## Left diagonal\n",
    "ax.plot((1 - d, 1 + d), (1 - d, 1 + d), transform=ax.transAxes, color=\"k\", clip_on=false,linewidth=0.8)  ## Right diagonal\n",
    "\n",
    "axes[1].scatter(x, y)\n",
    "\n",
    "fig"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Custom Time"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using Dates\n",
    "using PythonCall\n",
    "import PythonPlot as plt\n",
    "matplotlib = pyimport(\"matplotlib\")\n",
    "\n",
    "# Data\n",
    "dt = Dates.Hour(1)\n",
    "time = collect(DateTime(2014,11,20):dt:DateTime(2014,11,22))\n",
    "y = 10rand(length(time))\n",
    "dfmt = Dates.DateFormat(\"yyyy-mm-dd HH:MM:SS\")\n",
    "font1 = Dict(\"fontname\"=>\"Sans\",\"style\"=>\"normal\")\n",
    "# Convert time from milliseconds from day 0 to days from day 0\n",
    "time2 = Dates.value.(time)/1000/60/60/24\n",
    "\n",
    "timespan = \"\\n\" * Dates.format(minimum(time),dfmt) * \" - \" * Dates.format(maximum(time),dfmt)\n",
    "\n",
    "majorformatter = matplotlib.dates.DateFormatter(\"%d.%m.%Y\")\n",
    "minorformatter = matplotlib.dates.DateFormatter(\"%H:%M\")\n",
    "majorlocator = matplotlib.dates.DayLocator(interval=1)\n",
    "minorlocator = matplotlib.dates.HourLocator(byhour=(8, 16))\n",
    "\n",
    "# Plot\n",
    "\n",
    "fig, ax = plt.subplots(figsize=(8, 8))\n",
    "ax.plot_date(time2,y,linestyle=\"-\",marker=\"None\",label=\"test\")\n",
    "ax.axis(\"tight\")\n",
    "ax.set_title(\"Random Data Against Time\\n\" * timespan)\n",
    "\n",
    "ax.grid(\"on\")\n",
    "ax.set_xlabel(\"Time\")\n",
    "ax.set_ylabel(\"Stuff\",fontdict=font1)\n",
    "\n",
    "ax.xaxis.set_major_formatter(majorformatter)\n",
    "ax.xaxis.set_minor_formatter(minorformatter)\n",
    "ax.xaxis.set_major_locator(majorlocator)\n",
    "ax.xaxis.set_minor_locator(minorlocator)\n",
    "\n",
    "fig.autofmt_xdate(bottom=0.2,rotation=30,ha=\"right\")\n",
    "fig.set_tight_layout(true)\n",
    "fig"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Error bar"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using Dates\n",
    "import PythonPlot as plt\n",
    "\n",
    "x = collect(DateTime(2013,10,4):Dates.Day(1):DateTime(2013,10,9))\n",
    "y = [0.9;0.75;0.5;0.4;0.35;0.3]\n",
    "\n",
    "uppererror = [0.05 0.05 0.05 0.03 0.15 0.05;]\n",
    "lowererror = [0.15 0.2 0.05 0.1 0.05 0.05;]\n",
    "errs = [lowererror; uppererror]\n",
    "\n",
    "fig, ax = plt.subplots(figsize=(8, 8))\n",
    "ax.plot_date(x,y,linestyle=\"-\",label=\"Base Plot\") ## Basic line plot\n",
    "ax.errorbar(x,y,yerr=errs,fmt=\"o\") ## Plot irregular error bars\n",
    "ax.axis(\"tight\")\n",
    "\n",
    "ax.set_title(\"Error Bar Example\")\n",
    "ax.set_xlabel(\"Time\")\n",
    "ax.set_ylabel(\"Some Data\")\n",
    "ax.grid(\"on\")\n",
    "\n",
    "fig.autofmt_xdate(bottom=0.2,rotation=30,ha=\"right\") ## Autoformat the time format and rotate the labels so they don't overlap\n",
    "fig"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Histogram"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "import PythonPlot as plt\n",
    "\n",
    "x = randn(1000)\n",
    "nbins = 50\n",
    "fig, ax = plt.subplots()\n",
    "\n",
    "ax.hist(x,nbins)\n",
    "\n",
    "ax.grid(\"on\")\n",
    "ax.set_xlabel(\"X\")\n",
    "ax.set_ylabel(\"Y\")\n",
    "ax.set_title(\"Histogram\")\n",
    "\n",
    "fig"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Major and minor ticks"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using PythonCall\n",
    "import PythonPlot as plt\n",
    "matplotlib = pyimport(\"matplotlib\")\n",
    "\n",
    "x = collect(0.0:0.01:100.0)\n",
    "y = @. sin(0.1pi*x) * exp(-0.01x)\n",
    "\n",
    "fig, ax = plt.subplots(figsize=(8,8))\n",
    "\n",
    "ax.plot(x,y)\n",
    "ax.set_xlabel(\"X Axis\")\n",
    "ax.set_ylabel(\"Y Axis\")\n",
    "ax.grid(\"on\")\n",
    "ax.set_title(\"Major and Minor Ticks\")\n",
    "\n",
    "#  Set the tick interval\n",
    "Mx = matplotlib.ticker.MultipleLocator(20) ## Define interval of major ticks\n",
    "f = matplotlib.ticker.FormatStrFormatter(\"%1.2f\") ## Define format of tick labels\n",
    "ax.xaxis.set_major_locator(Mx) ## Set interval of major ticks\n",
    "ax.xaxis.set_major_formatter(f) ## Set format of tick labels\n",
    "\n",
    "mx = matplotlib.ticker.MultipleLocator(5) ## Define interval of minor ticks\n",
    "ax.xaxis.set_minor_locator(mx) ## Set interval of minor ticks\n",
    "\n",
    "My = matplotlib.ticker.MultipleLocator(0.5) ## Define interval of major ticks\n",
    "ax.yaxis.set_major_locator(My) ## Set interval of major ticks\n",
    "\n",
    "my = matplotlib.ticker.MultipleLocator(0.1) ## Define interval of minor ticks\n",
    "ax.yaxis.set_minor_locator(my) ## Set interval of minor ticks\n",
    "\n",
    "ax.xaxis.set_tick_params(which=\"major\",length=10,width=2,labelsize=16)\n",
    "ax.xaxis.set_tick_params(which=\"minor\",length=5,width=2)\n",
    "\n",
    "fig"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Multiple axis"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "import PythonPlot as plt\n",
    "\n",
    "n = 15\n",
    "x = collect(1:n)\n",
    "y1 = 10rand(n,1) .- 2\n",
    "y2 = 1000rand(n,1)\n",
    "y3 = 30rand(n,1) .- 15\n",
    "\n",
    "fig, ax = plt.subplots()\n",
    "\n",
    "ax.plot(x,y1,linestyle=\"-\",marker=\"o\",label=\"First\")\n",
    "ax.set_title(\"Multi-axis Plot\")\n",
    "ax.set_xlabel(\"X Axis\")\n",
    "font1 = Dict(\"color\"=>\"blue\")\n",
    "ax.set_ylabel(\"Left Axis\",fontdict=font1)\n",
    "plt.setp(ax.get_yticklabels(),color=\"blue\")\n",
    "\n",
    "new_position = [0.06;0.06;0.77;0.91]\n",
    "ax.set_position(new_position)\n",
    "\n",
    "# Create another axis on top of the current axis\n",
    "ax2 = ax.twinx()\n",
    "font2 = Dict(\"color\"=>\"purple\")\n",
    "ax2.set_ylabel(\"Right Axis\",fontdict=font2)\n",
    "ax2.plot(x,y2,color=\"purple\",linestyle=\"-\",marker=\"o\",label=\"Second\")\n",
    "ax2.set_position(new_position)\n",
    "plt.setp(ax2.get_yticklabels(),color=\"purple\")\n",
    "\n",
    "# Create another axis on top of the current axis\n",
    "ax3 = ax.twinx()\n",
    "ax3.spines[\"right\"].set_position((\"axes\",1.12)) ## Offset the y-axis label from the axis itself so it doesn't overlap the second axis\n",
    "font3 = Dict(\"color\"=>\"green\")\n",
    "ax3.set_ylabel(\"Far Right Axis\",fontdict=font3)\n",
    "ax3.plot(x,y3,color=\"green\",linestyle=\"-\",marker=\"o\",label=\"Third\")\n",
    "ax3.set_position(new_position)\n",
    "plt.setp(ax.get_yticklabels(),color=\"green\")\n",
    "\n",
    "ax.axis(\"tight\")\n",
    "\n",
    "# Enable just the right part of the frame\n",
    "ax3.set_frame_on(true) ## Make the entire frame visible\n",
    "ax3.patch.set_visible(false) ## Make the patch (background) invisible so it doesn't cover up the other axes' plots\n",
    "ax3.spines[\"top\"].set_visible(false) ## Hide the top edge of the axis\n",
    "ax3.spines[\"bottom\"].set_visible(false) ## Hide the bottom edge of the axis\n",
    "\n",
    "fig"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Sharing the Legend Box in Twin Axes"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "import PythonPlot as plt\n",
    "\n",
    "x1 = 1:10\n",
    "fig, ax1 = plt.subplots()\n",
    "l1 = ax1.plot(x1, x1, \"r-\")\n",
    "ax2 = ax1.twinx()\n",
    "l2 = ax2.plot(x1, exp.(x1), \"g-\")\n",
    "ax1.legend([first(l1), first(l2)], [\"x\", \"exp(x)\"])\n",
    "fig"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Pie Chart"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "import PythonPlot as plt\n",
    "\n",
    "labels = [\"Lager\",\"Pilsner\",\"Stout\",\"IPA\"]\n",
    "colors = [\"yellowgreen\",\"gold\",\"lightskyblue\",\"lightcoral\"]\n",
    "\n",
    "explode = zeros(length(labels))\n",
    "explode[2] = 0.1 ## Move slice 2 out by 0.1\n",
    "\n",
    "sizes = [15, 30, 45, 10]\n",
    "\n",
    "font = Dict(\"fontname\"=>\"Sans\",\"weight\"=>\"semibold\")\n",
    "\n",
    "fig, ax = plt.subplots()\n",
    "ax.pie(sizes,\n",
    "        labels=labels,\n",
    "        shadow=true,\n",
    "        startangle=90,\n",
    "        explode=explode,\n",
    "        colors=colors,\n",
    "        autopct=\"%1.1f%%\",\n",
    "        textprops=font)\n",
    "ax.axis(\"equal\")\n",
    "ax.set_title(\"Beer\")\n",
    "fig"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Quiver plots"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "import PythonPlot as plt\n",
    "R = -10:1:9\n",
    "X = [R;]'\n",
    "Y = [R;]\n",
    "U = repeat([R;]',length(X))\n",
    "V = repeat([R;],1,length(Y))\n",
    "\n",
    "fig, ax = plt.subplots(figsize=(8,8))\n",
    "q = ax.quiver(X,Y,U,V)\n",
    "ax.quiverkey(q,X=0.07,Y = 0.05, U = 10,coordinates=\"figure\", label=\"Quiver key, length = 10\",labelpos = \"E\")\n",
    "ax.set_title(\"Quiver Plot Example\")\n",
    "fig"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Scatter Plot"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "import PythonPlot as plt\n",
    "\n",
    "x = 100*rand(50)\n",
    "y = 100*rand(50)\n",
    "areas = 800*rand(50)\n",
    "\n",
    "fig, ax = plt.subplots(figsize=(8,8))\n",
    "ax.scatter(x,y,s=areas,alpha=0.5)\n",
    "ax.set_title(\"Scatter Plot\")\n",
    "ax.set_xlabel(\"X\")\n",
    "ax.set_ylabel(\"Y\")\n",
    "ax.grid(\"on\")\n",
    "fig"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Subplots"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "import PythonPlot as plt\n",
    "\n",
    "fig = plt.figure(\"PythonPlot_subplot_mixed\",figsize=(8,8)) ## Create a new blank figure\n",
    "##fig.set_figheight(7) # Does not work\n",
    "##fig.set_figwidth(3) # Doesn not work\n",
    "plt.subplot(221) ## Create the 1st axis of a 2x2 arrax of axes\n",
    "plt.grid(\"on\") ## Create a grid on the axis\n",
    "plt.title(\"221\") ## Give the most recent axis a title\n",
    "plt.subplot(222,polar=\"true\") ## Create a plot and make it a polar plot, 2nd axis of 2x2 axis grid\n",
    "plt.title(\"222\")\n",
    "ax = plt.subplot(223,polar=\"true\") ## Create a plot and make it a polar plot, 3rd axis of 2x2 axis grid\n",
    "ax.set_theta_zero_location(\"N\") ## Set 0 degrees to the top of the plot\n",
    "ax.set_theta_direction(-1) ## Switch the polar plot to clockwise\n",
    "plt.title(\"223\")\n",
    "plt.subplot(224) ## Create the 4th axis of a 2x2 arrax of axes\n",
    "plt.xlabel(\"This is an X axis\")\n",
    "plt.ylabel(\"This is a y axis\")\n",
    "plt.title(\"224\")\n",
    "fig.suptitle(\"2x2 Subplot\")\n",
    "\n",
    "fig"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Shared Axis"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "fig = plt.figure(\"PythonPlot_subplot_touching\",figsize=(8,8))\n",
    "plt.subplots_adjust(hspace=0.0) ## Set the vertical spacing between axes\n",
    "plt.subplot(311) ## Create the 1st axis of a 3x1 array of axes\n",
    "ax1 = plt.gca()\n",
    "ax1.set_xscale(\"log\") ## Set the x axis to a logarithmic scale\n",
    "plt.setp(ax1.get_xticklabels(),visible=false) ## Disable x tick labels\n",
    "plt.grid(\"on\")\n",
    "plt.title(\"Title\")\n",
    "plt.yticks(0.1:0.2:0.9) ## Set the y-tick range and step size, 0.1 to 0.9 in increments of 0.2\n",
    "plt.ylim(0.0,1.0) ## Set the y-limits from 0.0 to 1.0\n",
    "plt.subplot(312,sharex=ax1) ## Create the 2nd axis of a 3x1 array of axes\n",
    "ax2 = plt.gca()\n",
    "ax2.set_xscale(\"log\") ## Set the x axis to a logarithmic scale\n",
    "plt.setp(ax2.get_xticklabels(),visible=false) ## Disable x tick labels\n",
    "plt.grid(\"on\")\n",
    "plt.ylabel(\"Log Scale\")\n",
    "plt.yticks(0.1:0.2:0.9)\n",
    "plt.ylim(0.0,1.0)\n",
    "plt.subplot(313,sharex=ax2) ## Create the 3rd axis of a 3x1 array of axes\n",
    "ax3 = plt.gca()\n",
    "ax3.set_xscale(\"log\") ## Set the x axis to a logarithmic scale\n",
    "plt.grid(\"on\")\n",
    "plt.xlabel(\"Log Scale\")\n",
    "plt.yticks(0.1:0.2:0.9)\n",
    "plt.ylim(0.0,1.0)\n",
    "plt.suptitle(\"3x1 Shared Axis\")\n",
    "\n",
    "fig"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Surface plot"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using Distributions\n",
    "using LinearAlgebra\n",
    "import PythonPlot as plt\n",
    "plt.using3D() ## Needed to create a 3D subplot\n",
    "\n",
    "n = 100\n",
    "x = range(-3,stop=3,length=n)\n",
    "y = range(-3,stop=3,length=n)\n",
    "\n",
    "xgrid = repeat(x',n,1)\n",
    "ygrid = repeat(y,1,n)\n",
    "\n",
    "z = zeros(n,n)\n",
    "\n",
    "for i in 1:n\n",
    "    for j in 1:n\n",
    "        z[i:i,j:j] .= pdf(MvNormal(Matrix(1.0I,2,2)),[x[i];y[j]])\n",
    "    end\n",
    "end\n",
    "\n",
    "fig = plt.figure(\"PythonPlot_surfaceplot\",figsize=(8,8))\n",
    "\n",
    "ax = fig.add_subplot(2,1,1,projection=\"3d\")\n",
    "ax.plot_surface(xgrid, ygrid, z, rstride=2,edgecolors=\"k\", cstride=2, cmap=plt.ColorMap(\"gray\"), alpha=0.8, linewidth=0.25)\n",
    "ax.set_xlabel(\"X\")\n",
    "ax.set_ylabel(\"Y\")\n",
    "ax.set_title(\"Surface Plot\")\n",
    "\n",
    "ax = fig.add_subplot(2,1,2)\n",
    "cp = ax.contour(xgrid, ygrid, z, colors=\"black\", linewidth=2.0)\n",
    "ax.clabel(cp, inline=1, fontsize=10)\n",
    "ax.set_xlabel(\"X\")\n",
    "ax.set_ylabel(\"Y\")\n",
    "ax.set_title(\"Contour Plot\")\n",
    "fig.tight_layout()\n",
    "\n",
    "fig"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Windrose bar and line plots"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "import PythonPlot as plt\n",
    "theta = collect(0:2pi/30:2pi)\n",
    "r = rand(length(theta))\n",
    "width = 2pi/length(theta)\n",
    "\n",
    "#  Windrose Line Plot\n",
    "fig = plt.figure(figsize=(8,8)) ## Create a new figure\n",
    "ax = plt.axes(polar=\"true\") ## Create a polar axis\n",
    "\n",
    "ax.set_title(\"Wind Rose - Line\")\n",
    "ax.plot(theta,r,linestyle=\"-\",marker=\"None\") ## Basic line plot\n",
    "\n",
    "dtheta = 10\n",
    "\n",
    "ax.set_thetagrids(collect(0:dtheta:360-dtheta)) ## Show grid lines from 0 to 360 in increments of dtheta\n",
    "ax.set_theta_zero_location(\"N\") ## Set 0 degrees to the top of the plot\n",
    "ax.set_theta_direction(-1) ## Switch to clockwise\n",
    "\n",
    "fig"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "fig = plt.figure(\"PythonPlot_windrose_barplot\",figsize=(8,8)) ## Create a new figure\n",
    "ax = plt.axes(polar=\"true\") ## Create a polar axis\n",
    "ax.set_title(\"Wind Rose - Bar\")\n",
    "ax.bar(theta,r,width=width) ## Bar plot\n",
    "\n",
    "dtheta = 10\n",
    "ax.set_thetagrids(collect(0:dtheta:360-dtheta)) ## Show grid lines from 0 to 360 in increments of dtheta\n",
    "ax.set_theta_zero_location(\"N\") ## Set 0 degrees to the top of the plot\n",
    "ax.set_theta_direction(-1) ## Switch to clockwise\n",
    "\n",
    "fig"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.10.2",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 3
}
