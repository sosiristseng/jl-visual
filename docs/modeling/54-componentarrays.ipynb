{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "0a227414",
   "metadata": {},
   "source": [
    "# ComponentArrays\n",
    "\n",
    "https://github.com/SciML/ComponentArrays.jl are array blocks that can be accessed through a named index.\n",
    "\n",
    "You can compose differential equations without a DSL like `ModelingToolkit`."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a0489bed",
   "metadata": {},
   "outputs": [],
   "source": [
    "using ComponentArrays\n",
    "using OrdinaryDiffEq\n",
    "using SimpleUnPack: @unpack"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "38eb4d7b",
   "metadata": {},
   "source": [
    "## Composing Lorenz and Lotka-Volterra systems"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "33e36694",
   "metadata": {},
   "source": [
    "### Lorenz system"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "8cb69c0b",
   "metadata": {},
   "outputs": [],
   "source": [
    "function lorenz!(D, u, p, t; f=0.0)\n",
    "    @unpack σ, ρ, β = p\n",
    "    @unpack x, y, z = u\n",
    "\n",
    "    D.x = σ*(y - x)\n",
    "    D.y = x*(ρ - z) - y - f\n",
    "    D.z = x*y - β*z\n",
    "    return nothing\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b71b3657",
   "metadata": {},
   "outputs": [],
   "source": [
    "tspan = (0.0, 20.0)\n",
    "lorenz_p = (σ=10.0, ρ=28.0, β=8/3)\n",
    "lorenz_ic = ComponentArray(x=1.0, y=0.0, z=0.0)\n",
    "lorenz_prob = ODEProblem(lorenz!, lorenz_ic, tspan, lorenz_p)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "128fef53",
   "metadata": {},
   "source": [
    "### Lotka-Volterra system"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "394b6f13",
   "metadata": {},
   "outputs": [],
   "source": [
    "function lotka!(D, u, p, t; f=0.0)\n",
    "    @unpack α, β, γ, δ = p\n",
    "    @unpack x, y = u\n",
    "\n",
    "    D.x =  α*x - β*x*y + f\n",
    "    D.y = -γ*y + δ*x*y\n",
    "    return nothing\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "97dd4b19",
   "metadata": {},
   "outputs": [],
   "source": [
    "lotka_p = (α=2/3, β=4/3, γ=1.0, δ=1.0)\n",
    "lotka_ic = ComponentArray(x=1.0, y=1.0)\n",
    "lotka_prob = ODEProblem(lotka!, lotka_ic, tspan, lotka_p)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e0a677bf",
   "metadata": {},
   "source": [
    "### Composed system"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "02a7bcc6",
   "metadata": {},
   "outputs": [],
   "source": [
    "function composed!(D, u, p, t)\n",
    "    c = p.c\n",
    "    @unpack lorenz, lotka = u\n",
    "\n",
    "    lorenz!(D.lorenz, lorenz, p.lorenz, t, f=c*lotka.x)\n",
    "    lotka!(D.lotka, lotka, p.lotka, t, f=c*lorenz.x)\n",
    "    return nothing\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "1df342cc",
   "metadata": {},
   "outputs": [],
   "source": [
    "comp_p = (lorenz=lorenz_p, lotka=lotka_p, c=0.01)\n",
    "comp_ic = ComponentArray(lorenz=lorenz_ic, lotka=lotka_ic)\n",
    "comp_prob = ODEProblem(composed!, comp_ic, tspan, comp_p)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c77dc98e",
   "metadata": {},
   "source": [
    "### Solve problem\n",
    "We can solve the composed system..."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d79ef956",
   "metadata": {},
   "outputs": [],
   "source": [
    "@time comp_sol = solve(comp_prob);"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f6c39925",
   "metadata": {},
   "source": [
    "Extract a certain state variable."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "8fb1cd90",
   "metadata": {},
   "outputs": [],
   "source": [
    "map(comp_sol(0:0.1:20).u) do u\n",
    "    u.lotka.x\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "cc8dcf94",
   "metadata": {},
   "source": [
    "or we can unit test one of the component systems"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "dd4eb78c",
   "metadata": {},
   "outputs": [],
   "source": [
    "lotka_sol = solve(lotka_prob)\n",
    "lotka_sol(1.0).x"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "ccdaa6c8",
   "metadata": {},
   "source": [
    "## Symbolic mapping in `ODEFunction`\n",
    "\n",
    "- using `syms=...`.\n",
    "- Only valid in non-nested `ComponentArrays`."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "9ca907c5",
   "metadata": {},
   "outputs": [],
   "source": [
    "lotka_func = ODEFunction(lotka!; syms=keys(lotka_ic))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "561b35e2",
   "metadata": {},
   "outputs": [],
   "source": [
    "@time lab_sol = solve(ODEProblem(lotka_func, lotka_ic, tspan, lotka_p));"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "292210ef",
   "metadata": {},
   "outputs": [],
   "source": [
    "lab_sol(0.0:0.1:20, idxs=:x)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.12.2",
   "language": "julia",
   "name": "julia-1.12"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
